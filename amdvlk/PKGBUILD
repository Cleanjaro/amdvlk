# Maintainer:  David Spink <yorper_protonmail.com>
# Contributor: Christoph Haag <haagch@studi.informatik.uni-stuttgart.de>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

# Read default.xml at https://github.com/GPUOpen-Drivers/AMDVLK
# for the correct commit strings for the current stable build information.

pkgname=amdvlk
amdvlk_commit=ddb9bc059a7f6898d0f915672ec4926a8640b0a6
xgl_commit=9b632ef4f132bddc94769702ed8b49efbc39d89c
pal_commit=66e78b997748d03d77e1d706c10f1f17e18e5654
llpc_commit=0da6ca8e09f41639636a106f9b9ca74df50321ce
spvgen_commit=2f31d1170e8a12a66168b23235638c4bbc43ecdc
llvm_commit=9bc5dd4450a6361faf5c5661056a7ee494fad830
pkgver=2019.Q3.4
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
depends=('vulkan-icd-loader')
provides=('vulkan-amdvlk' 'vulkan-driver')
conflicts=('vulkan-amdvlk')
makedepends=('dri2proto' 'xorg-server-devel' 'cmake' 'python' 'libxml2' 'wayland' 'libdrm' 'libxrandr')
source=(AMDVLK-${amdvlk_commit}.tar.gz::$url/AMDVLK/archive/${amdvlk_commit}.tar.gz
        xgl-${xgl_commit}.tar.gz::$url/xgl/archive/${xgl_commit}.tar.gz
        pal-${pal_commit}.tar.gz::$url/pal/archive/${pal_commit}.tar.gz
        llpc-${llpc_commit}.tar.gz::$url/llpc/archive/${llpc_commit}.tar.gz
        spvgen-${spvgen_commit}.tar.gz::$url/spvgen/archive/${spvgen_commit}.tar.gz
        llvm-${llvm_commit}.tar.gz::$url/llvm/archive/${llvm_commit}.tar.gz
        gcc9-fix.patch)
sha256sums=('06fac807fd9c4b951188a081641abcb575b1e98d205ec7ff390644a18fcc951b'
            '6baede0581d63bdf88cc72c8c496b0cef534ee244dd4f74bcb39a9e0e0beeacd'
            'e8a29b20a166cb7139157a40e88b8087ed33097871dcaee1bfda61b6a417886f'
            '00bf10ff483ba5f6420fb3bfe34f7ce77c6d33515840df64ed1e2858e0eeb831'
            'cc946ad2835e502aca904c5f87802a2004eaed4729cb5c1dc29a5258d1c1e401'
            'efbde2752044ec74d522c160899491105dbc77bb8a08ff64c274d2b94a6916d1'
            'fdb5437052043cd2d35544876c835138ddde8ba5f90ec15c960a962e96490687')

prepare() {
  ln -s AMDVLK-${amdvlk_commit} AMDVLK
  ln -s xgl-${xgl_commit} xgl
  ln -s pal-${pal_commit} pal
  ln -s llpc-${llpc_commit} llpc
  ln -s spvgen-${spvgen_commit} spvgen
  ln -s llvm-${llvm_commit} llvm
  touch amdPalSettings.cfg

  # Fix compiling with GCC9, Thanks void linux team
  patch -Np1 -i gcc9-fix.patch
}

build() {
  export CFLAGS="$CFLAGS -fno-plt"
  export CXXFLAGS="$CXXFLAGS -fno-plt"
  export LDFLAGS="$LDFLAGS -z now"

  msg "building xgl..."
  cd xgl
  cmake -H. -Bbuilds/Release64 \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_XLIB_XRANDR_SUPPORT=On \
    -DBUILD_WAYLAND_SUPPORT=On

  cd builds/Release64
  make
  msg "building xgl finished!"
}

package() {
  install -m755 -d "${pkgdir}"/usr/lib
  install -m755 -d "${pkgdir}"/usr/share/vulkan/icd.d
  install -m755 -d "${pkgdir}"/usr/share/licenses/amdvlk
  install -m755 -d "${pkgdir}"/etc/amd

  install xgl/builds/Release64/icd/amdvlk64.so "${pkgdir}"/usr/lib/
  install AMDVLK/json/Redhat/amd_icd64.json "${pkgdir}"/usr/share/vulkan/icd.d/
  install AMDVLK/LICENSE.txt "${pkgdir}"/usr/share/licenses/amdvlk/
  install amdPalSettings.cfg "${pkgdir}"/etc/amd/

  sed -i "s/\/lib64/\/lib/g" "${pkgdir}"/usr/share/vulkan/icd.d/amd_icd64.json
}
