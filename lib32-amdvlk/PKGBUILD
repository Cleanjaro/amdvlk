# Maintainer:  David Spink <yorper_protonmail.com>
# Contributor: Christoph Haag <haagch@studi.informatik.uni-stuttgart.de>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

# Read default.xml at https://github.com/GPUOpen-Drivers/AMDVLK
# for the correct commit strings for the current stable build information.

pkgname=lib32-amdvlk
amdvlk_commit=53d1a7c2970ac7a576d0118323d7b8b081e8c8ba
xgl_commit=6b0fbc836e01be4b8fb1990b4e31846ec19305bc
pal_commit=46251728a7137f399b564b0886bd9bf9888c48ef
llpc_commit=f60ebe48d60ef00e69b399a24e04d600cdcc1f0c
spvgen_commit=2f31d1170e8a12a66168b23235638c4bbc43ecdc
llvm_commit=951ae36dc33d628235446a7abe87a1aebf6717e8
pkgver=2019.Q3.3
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver (32-bit)"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
depends=('lib32-vulkan-icd-loader')
provides=('lib32-vulkan-amdvlk' 'lib32-vulkan-driver')
conflicts=('lib32-vulkan-amdvlk')
makedepends=('dri2proto' 'xorg-server-devel' 'cmake' 'python' 'lib32-libxml2' 'lib32-libdrm' 'libxrandr')
source=(AMDVLK-${amdvlk_commit}.tar.gz::$url/AMDVLK/archive/${amdvlk_commit}.tar.gz
        xgl-${xgl_commit}.tar.gz::$url/xgl/archive/${xgl_commit}.tar.gz
        pal-${pal_commit}.tar.gz::$url/pal/archive/${pal_commit}.tar.gz
        llpc-${llpc_commit}.tar.gz::$url/llpc/archive/${llpc_commit}.tar.gz
        spvgen-${spvgen_commit}.tar.gz::$url/spvgen/archive/${spvgen_commit}.tar.gz
        llvm-${llvm_commit}.tar.gz::$url/llvm/archive/${llvm_commit}.tar.gz
        gcc9-fix.patch)
sha256sums=('8a634a8764cf2835b16b3adedebd3bda7033ed73c5b2af6b63cf1a7e7773efda'
            'fee95d0a55cbd9c34aa54c3309ad896d56c287f35bc7cd6bb25bd86f7e0985d6'
            '6832551a933fd04d8b1d031cf49b86497c17c878675fc641df23c9c9e128d52b'
            '4d6b623092215bfb8f9f01601db3de2b93cc0bf305173bafeb58c65f32967043'
            'cc946ad2835e502aca904c5f87802a2004eaed4729cb5c1dc29a5258d1c1e401'
            '50b25878e19cd8a7d15aad891463fbdac60cbb04b2a4027bc31ff9ca3da4a5f6'
            'fdb5437052043cd2d35544876c835138ddde8ba5f90ec15c960a962e96490687')

prepare() {
  ln -s AMDVLK-${amdvlk_commit} AMDVLK
  ln -s xgl-${xgl_commit} xgl
  ln -s pal-${pal_commit} pal
  ln -s llpc-${llpc_commit} llpc
  ln -s spvgen-${spvgen_commit} spvgen
  ln -s llvm-${llvm_commit} llvm

  # Fix compiling with GCC9, Thanks void linux team
  patch -Np1 -i gcc9-fix.patch
}

build() {
  export CFLAGS="$CFLAGS -fno-plt"
  export CXXFLAGS="$CXXFLAGS -fno-plt"
  export LDFLAGS="$LDFLAGS -z now"

  msg "building xgl..."
  cd xgl
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cmake -H. -Bbuilds/Release \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_XLIB_XRANDR_SUPPORT=On \
    -DBUILD_WAYLAND_SUPPORT=On \
    -DCMAKE_C_FLAGS=-m32 \
    -DCMAKE_CXX_FLAGS=-m32 \
    -DLLVM_TARGET_ARCH:STRING=i686 \
    -DLLVM_DEFAULT_TARGET_TRIPLE="i686-pc-linux-gnu"

  cd builds/Release
  make
  msg "building xgl finished!"
}

package() {
  install -m755 -d "${pkgdir}"/usr/lib32
  install -m755 -d "${pkgdir}"/usr/share/vulkan/icd.d
  install -m755 -d "${pkgdir}"/usr/share/licenses/lib32-amdvlk

  install xgl/builds/Release/icd/amdvlk32.so "${pkgdir}"/usr/lib32/
  install AMDVLK/json/Redhat/amd_icd32.json "${pkgdir}"/usr/share/vulkan/icd.d/
  install AMDVLK/LICENSE.txt "${pkgdir}"/usr/share/licenses/lib32-amdvlk/
  
  sed -i "s/\/lib/\/lib32/g" "${pkgdir}"/usr/share/vulkan/icd.d/amd_icd32.json
}
