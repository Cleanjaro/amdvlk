# Maintainer:  David Spink <yorper_protonmail.com>
# Contributor: Christoph Haag <haagch@studi.informatik.uni-stuttgart.de>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

# Read default.xml at https://github.com/GPUOpen-Drivers/AMDVLK
# for the correct commit strings for the current stable build information.

pkgname=lib32-amdvlk
amdvlk_commit=4f7b640d4f2ca870ddc7a474ac625cc7f253d5bd
xgl_commit=2315f2a4de4df53eaa54b56e96332687ed12269f
pal_commit=ffb21e86238819817075e252ad9b49ea5284968d
llpc_commit=252b93409b7cc92dacf48a421b9aac2a77629381
spvgen_commit=051aaa46272aae7808129b98fd9da49f3229eb78
llvm_commit=d9e4934189845f30428cad6da16e8745540291db
pkgver=2019.Q3.2
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver (32-bit)"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
depends=('lib32-vulkan-icd-loader')
provides=('lib32-vulkan-amdvlk' 'lib32-vulkan-driver')
conflicts=('lib32-vulkan-amdvlk')
makedepends=('dri2proto' 'xorg-server-devel' 'cmake' 'python' 'lib32-libxml2' 'lib32-libdrm' 'libxrandr')
source=(AMDVLK-${amdvlk_commit}.tar.gz::$url/AMDVLK/archive/${amdvlk_commit}.tar.gz
        xgl-${xgl_commit}.tar.gz::$url/xgl/archive/${xgl_commit}.tar.gz
        pal-${pal_commit}.tar.gz::$url/pal/archive/${pal_commit}.tar.gz
        llpc-${llpc_commit}.tar.gz::$url/llpc/archive/${llpc_commit}.tar.gz
        spvgen-${spvgen_commit}.tar.gz::$url/spvgen/archive/${spvgen_commit}.tar.gz
        llvm-${llvm_commit}.tar.gz::$url/llvm/archive/${llvm_commit}.tar.gz)
sha256sums=('b6db2aa1c97a31b00c6771de0e89c3712b9eed149523ae9a5dc4d64a27dbe542'
            '1b01201e15eb62ad3df8b925de5763484570a85a4cdf686efe066e26d0ca70c2'
            'd0b96d0b37ac24ada16fa030fe80070f39528f69df1c64c3474c3f58b98475ee'
            'eb0a94288e4bda889d75e287247e63b97a91d9761fb818c9106d4afa490760c3'
            '6f1242b24197e8f6ff259371773bedcb858dec379eba32f07d150d6eb57e5a2b'
            'a816af55d118d1ab3f96729c2401568e689190d10388aa4f067917061bbfd271')

prepare() {
  ln -s AMDVLK-${amdvlk_commit} AMDVLK
  ln -s xgl-${xgl_commit} xgl
  ln -s pal-${pal_commit} pal
  ln -s llpc-${llpc_commit} llpc
  ln -s spvgen-${spvgen_commit} spvgen
  ln -s llvm-${llvm_commit} llvm

  # Disable -Werror, https://github.com/GPUOpen-Drivers/AMDVLK/issues/89
  for i in xgl/icd/CMakeLists.txt llpc/CMakeLists.txt llpc/imported/metrohash/CMakeLists.txt llvm/utils/benchmark/CMakeLists.txt llvm/utils/benchmark/test/CMakeLists.txt pal/src/core/imported/addrlib/CMakeLists.txt pal/src/core/imported/vam/CMakeLists.txt pal/shared/gpuopen/cmake/AMD.cmake
  do
    sed -i "s/-Werror//g" "$srcdir"/$i
  done
}

build() {
  export CFLAGS="$CFLAGS -fno-plt"
  export CXXFLAGS="$CXXFLAGS -fno-plt"
  export LDFLAGS="$LDFLAGS -z now"

  msg "building xgl..."
  cd xgl
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cmake -H. -Bbuilds/Release \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_XLIB_XRANDR_SUPPORT=On \
    -DBUILD_WAYLAND_SUPPORT=On \
    -DCMAKE_C_FLAGS=-m32 \
    -DCMAKE_CXX_FLAGS=-m32 \
    -DLLVM_TARGET_ARCH:STRING=i686 \
    -DLLVM_DEFAULT_TARGET_TRIPLE="i686-pc-linux-gnu"

  cd builds/Release
  make
  msg "building xgl finished!"
}

package() {
  install -m755 -d "${pkgdir}"/usr/lib32
  install -m755 -d "${pkgdir}"/usr/share/vulkan/icd.d
  install -m755 -d "${pkgdir}"/usr/share/licenses/lib32-amdvlk

  install xgl/builds/Release/icd/amdvlk32.so "${pkgdir}"/usr/lib32/
  install AMDVLK/json/Redhat/amd_icd32.json "${pkgdir}"/usr/share/vulkan/icd.d/
  install AMDVLK/LICENSE.txt "${pkgdir}"/usr/share/licenses/lib32-amdvlk/
  
  sed -i "s/\/lib/\/lib32/g" "${pkgdir}"/usr/share/vulkan/icd.d/amd_icd32.json
}
