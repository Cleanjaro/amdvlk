# Maintainer:  David Spink <yorper_protonmail.com>
# Contributor: Christoph Haag <haagch@studi.informatik.uni-stuttgart.de>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

# Read default.xml at https://github.com/GPUOpen-Drivers/AMDVLK
# for the correct commit strings for the current stable build information.

pkgname=lib32-amdvlk
amdvlk_commit=bff1e718f0d2fecf767495841e78deeb7e484347
xgl_commit=eee58c8e482ac4a6fdc40452cb4ad744395d0f74
pal_commit=6c8eaa257e6216437fdfe3f17d418eccfe42e0bd
llpc_commit=aa8a9d7f2b7ad7b81b70e7959e99e3f31f85c211
spvgen_commit=53245b96b7a647743f50b9d841751f9755002661
llvm_commit=8118692e8aefbd95f791ff49beddb8ce90e8927c
pkgver=2019.Q3.1
pkgrel=1
pkgdesc="AMD's standalone Vulkan driver (32-bit)"
arch=(x86_64)
url="https://github.com/GPUOpen-Drivers"
license=('MIT')
depends=('lib32-vulkan-icd-loader')
provides=('lib32-vulkan-amdvlk' 'lib32-vulkan-driver')
conflicts=('lib32-vulkan-amdvlk')
makedepends=('dri2proto' 'xorg-server-devel' 'cmake' 'python' 'lib32-libxml2' 'lib32-libdrm' 'libxrandr')
source=(AMDVLK-${amdvlk_commit}.tar.gz::$url/AMDVLK/archive/${amdvlk_commit}.tar.gz
        xgl-${xgl_commit}.tar.gz::$url/xgl/archive/${xgl_commit}.tar.gz
        pal-${pal_commit}.tar.gz::$url/pal/archive/${pal_commit}.tar.gz
        llpc-${llpc_commit}.tar.gz::$url/llpc/archive/${llpc_commit}.tar.gz
        spvgen-${spvgen_commit}.tar.gz::$url/spvgen/archive/${spvgen_commit}.tar.gz
        llvm-${llvm_commit}.tar.gz::$url/llvm/archive/${llvm_commit}.tar.gz)
sha256sums=('323c62305565093958cd2d6ac0b58ed7c3787c1d38d255f77ee1e3bf3f09edd4'
            '828bf0936e0efc86c40e3abb6f92cc214a493ab8e2ff2054d455bab399ae2d0a'
            '83a4940d3bd64f38a83e14d7225cce6b2234610dbe6e3d55c50e1adc6126b2d2'
            'f7efe91134588233e2d3f25ff53068fb0b7c6d144fabf907c7b8c807efe635e8'
            '253bfaf1fc33c8ef01ca1681a5ea7400fd4ab7373c48e4f79beb10d89455d307'
            'b0cbcd4d83530ad5bd515642f903837e6cc308eb68da51900208acf9f2dbe351')

prepare() {
  ln -s AMDVLK-${amdvlk_commit} AMDVLK
  ln -s xgl-${xgl_commit} xgl
  ln -s pal-${pal_commit} pal
  ln -s llpc-${llpc_commit} llpc
  ln -s spvgen-${spvgen_commit} spvgen
  ln -s llvm-${llvm_commit} llvm

  # Disable -Werror, https://github.com/GPUOpen-Drivers/AMDVLK/issues/89
  for i in xgl/icd/CMakeLists.txt llpc/CMakeLists.txt llpc/imported/metrohash/CMakeLists.txt llvm/utils/benchmark/CMakeLists.txt llvm/utils/benchmark/test/CMakeLists.txt pal/src/core/imported/addrlib/CMakeLists.txt pal/src/core/imported/vam/CMakeLists.txt pal/shared/gpuopen/cmake/AMD.cmake
  do
    sed -i "s/-Werror//g" "$srcdir"/$i
  done
}

build() {
  msg "building xgl..."
  cd xgl
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cmake -H. -Bbuilds/Release \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_WAYLAND_SUPPORT=On \
    -DCMAKE_C_FLAGS=-m32 \
    -DCMAKE_CXX_FLAGS=-m32 \
    -DLLVM_TARGET_ARCH:STRING=i686 \
    -DLLVM_DEFAULT_TARGET_TRIPLE="i686-pc-linux-gnu"

  cd builds/Release
  make
  msg "building xgl finished!"
}

package() {
  install -m755 -d "${pkgdir}"/usr/lib32
  install -m755 -d "${pkgdir}"/usr/share/vulkan/icd.d
  install -m755 -d "${pkgdir}"/usr/share/licenses/lib32-amdvlk

  install xgl/builds/Release/icd/amdvlk32.so "${pkgdir}"/usr/lib32/
  install AMDVLK/json/Redhat/amd_icd32.json "${pkgdir}"/usr/share/vulkan/icd.d/
  install AMDVLK/LICENSE.txt "${pkgdir}"/usr/share/licenses/lib32-amdvlk/
  
  sed -i "s/\/lib/\/lib32/g" "${pkgdir}"/usr/share/vulkan/icd.d/amd_icd32.json
}
